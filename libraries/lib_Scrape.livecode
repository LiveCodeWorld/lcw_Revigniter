script "lib_Scrape"
--> MetaData
-
license: GPLv3
name: lib_Scrape
type: library
version: 0.4


--> Working on
-
command scrape_SetUserAgent
   -- HTTP_USER_AGENT Mozilla/5.0 (Macintosh; Intel Mac OS X 10.13; rv:62.0) Gecko/20100101 Firefox/62.0
   -- HTTP_USER_AGENT Mozilla/5.0 (Macintosh; Intel Mac OS X 10_13_6) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/68.0.3440.106 Safari/537.36
   put "User-Agent: Mozilla/5.0 (Macintosh; Intel Mac OS X 10.13; rv:62.0) Gecko/20100101 Firefox/62.0" into sHeader
   set the httpheaders to sHeader
end scrape_SetUserAgent

function scrape_ConstructFedwikiPageArray someURL, pAddFaviconLink, pAddCitation, pAddMarkDownLinks
   -- uses "lib_Scrape"
   local pageArray
   
   -- revXMLDeleteAllTrees
   -- put html_FetchTidy (someURL) into someHTML -- tidy needs updating on server
   -- put url someURL into someHTML
   put html_Fetch (someURL) into someHTML -- tidy needs updating on server
   if someHTML is empty then
      put "Could not fetch any html for:" && someURL into someMarkdown
      put pageArray_Construct ("Error", someMarkdown) into pageArray
      return pageArray
   end if
   --
   put embedly_FetchArray (someUrl, 420) into embedlyArray
   put embedlyArray ["title"] into pageTitle
   if embedlyArray is an array then
      put embedlyArray ["description"] into pageDescription
      put embedlyArray ["thubnail_url"] into imageURL
      put embedlyArray ["html"] into iFrameHtml
   else
      put scrape_GetHeaderMetadataArray (someHTML) into headerArray
      _ScrapeSetPageInfo headerArray, pageTitle, pageDescription, imageURL
   end if
   
   -- create page and image array
   fedwiki_AddExternalLink pageDescription, someURL
   put pageArray_ConstructImage (imageURL, pageTitle, pageDescription, someURL, pageTitle) into pageArray
   --
   if pAddFaviconLink is true then
      put html_ConstructFaviconLink (headerSection, someURL) into htmlLine
      if htmlLine is not empty then
         pageArray_AddHtml pageArray, htmlLine
      end if
   end if
   
   -- add citiation
   if pAddCitation is true then
      put doi_FetchCitation (someURL) into someCitation
      if someCitation is not empty then
         pageArray_AddMarkdown pageArray, "# Citations"
         html_AddTag someCitation, "blockquote"
         pageArray_AddHtml pageArray, someCitation
      end if
   end if
   
   -- add # Links section
   if pAddMarkDownLinks is not false then
      put fedwiki_ConstructMarkdownLine (pageTitle, someURL) into someMarkdown
      pageArray_AddMarkdown pageArray, "# Links"
      pageArray_AddMarkdown pageArray, someMarkdown
   end if
   --
   return pageArray
end scrape_ConstructFedwikiPageArray


--> Embedly
-
function embedly_FetchArray someUrl, pWidth
   put embedly_GetApiKey() into EmbedlyApiKey
   
   if pWidth is empty then put 420 into pWidth
   
   put urlEncode (someUrl) into encodedUrl
   put merge ("http://api.embed.ly/1/oembed?key=[[EmbedlyApiKey]]&url=[[encodedUrl]]") into someURL
   if mWidth is not empty then put "&maxwidth=" & pWidth after someURL
   put url someURL into someJSON
   put json_ToArray (someJSON) into someArray
   return someArray
end embedly_FetchArray

function embedly_GetApiKey
   put secret_Get ("EmbedlyApiKey") into apiKey
   return apiKey 
end embedly_GetApiKey


--> Scrape
-
function scrape_GetHeaderMetadataArray someHTML
   -- regular expressions this uses are weak
   put html_GetHead (someHTML) into headerSection
   --
   put html_ExtractHeaderTitle (headerSection) into headerMetadataArray ["title"]
   put html_ExtractHeaderDescription (headerSection) into headerMetadataArray ["description"]
   --
   put scrape_GetJsonLdArray (headerSection) into headerMetadataArray ["jsonLD"]
   --
   if headerSection contains "twitter:" then
      put "card,description,title,image,creator,domain" into propNames
      repeat for each item propName in propNames
         put scrape_GetTwitterContent (propName, headerSection) into metaContent
         put metaContent into headerMetadataArray ["twitter"][propName]
      end repeat
   end if
   --
   if headerSection contains "og:" then
      put "locale,type,title,description,url,site_name,image,image:secure_url" into propNames
      repeat for each item propName in propNames
         put scrape_GetOgContent (propName, headerSection) into metaContent
         put metaContent into headerMetadataArray ["og"][propName]
      end repeat
   end if
   --
   return headerMetadataArray
end scrape_GetHeaderMetadataArray

function scrape_GetTreeHeaderMetadataArray someHTML, pTidyIsTrue
   put html_GetHead (someHTML) into headerSection
   put xml_ToArray (headerSection) into headerPageArray
   /*
   if pTidyIsTrue then html_Tidy someHtml
   put revXMLCreateTree (someHTML, false, true, false) into treeID
   if treeID is not a number then return treeID
   put scrapeID_ConstructHeaderPageArray (treeID) into headerPageArray
   revXMLDeleteTree treeID
   */
   return headerPageArray
end scrape_GetTreeHeaderMetadataArray


--> ScrapeID
-
-- does not work with nodes that do not close like "meta"

command scrapeID_OpenGraph treeID, @metaTitle, @metaImageURL, @metaDescription, @pageURL
   -- put revXMLNodeContents (treeID, "html/head/title") into htmlTitle
   put revXMLMatchingNode (treeID, "html/head", "meta", "property", "og:title", -1, false) into titleNode
   put word 1 to -1 of revXMLAttribute (treeID, titleNode, "content") into metaTitle
   
   put revXMLMatchingNode (treeID, "html/head", "meta", "property", "og:image", -1, false) into imageNode
   put word 1 to -1 of revXMLAttribute (treeID, imageNode, "content") into metaImageURL
   
   put revXMLMatchingNode (treeID, "html/head", "meta", "property", "og:description", -1, false) into descriptionNode
   put word 1 to -1 of revXMLAttribute (treeID, descriptionNode, "content") into metaDescription
   
   put revXMLMatchingNode (treeID, "html/head", "meta", "property", "og:url", -1, false) into urlNode
   put word 1 to -1 of revXMLAttribute (treeID, urlNode, "content") into pageURL
end scrapeID_OpenGraph

function scrape_HeaderSection xHTML
   -- does not work with nodes that do not close like "meta"
   -- unless we use tidy
   -- revXMLDeleteAllTrees
   put html_GetHead (xHTML) into headerXML
   
   put revXMLCreateTree (headerXML, false, true, false) into treeID
   put revXMLNodeContents (treeID, "head/title") into metaArray ["title"]
   put revXMLChildNames (treeID, "head", CR, "meta", true) into childNodes
   
   -- only meta tags
   -- oftern returns "xmlerr, can't find element as cant find name or construct meaningful array"
   repeat for each line childNode in childNodes
      put "head/" & childNode into someNode
      put revXMLAttributes (treeID, someNode, tab, CR) into attributeArray
      split attributeArray using CR and tab
      --
      put attributeArray ["name"] into metaName
      if metaName is not empty then
         delete variable attributeArray ["name"]
         put attributeArray into metaArray [metaName]
      end if
      
      --
      put attributeArray ["property"] into propertyName
      if propertyName is not empty then
         delete variable attributeArray ["property"]
         put attributeArray into metaArray ["property"][propertyName]
      end if
      
      -- put revXMLNextSibling (treeID, childNode) into childNode
      -- if childNode is empty then exit repeat
   end repeat
   
   -- put revXMLNodeContents (treeID, "head/title") into htmlTitle
   -- scrapeID_OpenGraph treeID, metaTitle, metaImageURL, metaDescription, pageURL
   --
   revXMLDeleteTree treeID
   return metaArray
end scrape_HeaderSection


--> Scrape | Without | XML
-
function scrape_GetJsonLdArray someHTML
   get scrape_GetJsonLD (someHTML)
   if it is empty then return empty
   put json_ToArray (it) into jsonLdArray
   if it is false then return empty
   --
   return jsonLdArray
end scrape_GetJsonLdArray

function scrape_GetJsonLD someHTML
   /*
   <script type='application/ld+json' class='yoast-schema-graph yoast-schema-graph--main'>{"@context":"https://schema.org",...</script>
   */
   put merge ("<script\stype='application/ld\+json'\sclass='.+'>(.*)</script>") into someReg
   --
   if matchText (someHTML, someReg, metaContent) is true then
      return metaContent
   else
      return empty
   end if
end scrape_GetJsonLD

function scrape_GetScriptTag sType, someHTML
   /*
   <script type='application/ld+json' ... </script>
   */
   --
   put merge ("(<script\stype='[[sType]]'.*</script>)") into someReg
   -- replace "'" with quote in someReg
   --
   if matchText (someHTML, someReg, metaContent) is true then
      return metaContent
   else
      return empty
   end if
end scrape_GetScriptTag

function scrape_GetOgContent propName, someHTML
   /*
   <meta property="og:locale" content="en_US" />
   <meta property="og:type" content="article" />
   <meta property="og:title" content="Form Builder Pricing | Pabbly" />
   <meta property="og:description" content="We don&#8217;t restrict you on any feature! Yes, everything is unlimited. No restriction on payments, submission, users and much more. Yearly (Get 4 Months Free) Monthly 1 Form Package $10 / mo Per Form Buy Now View All Features 3 Form Package $8 / mo Per Form Buy Now View All Features 10 Form Package &hellip; Continue reading &quot;Form Builder Pricing&quot;" />
   <meta property="og:url" content="https://www.pabbly.com/form-builder/pricing/" />
   <meta property="og:site_name" content="Pabbly" />
   <meta property="og:image" content="https://www.pabbly.com/wp-content/uploads/2019/07/formbuilder.png" />
   <meta property="og:image:secure_url" content="https://www.pabbly.com/wp-content/uploads/2019/07/formbuilder.png" />
   */
   
   put merge ("(?miU)<meta\s+property='og:[[propName]]'\s+content='(.*)'\s*/>") into someReg
   put "['" & quote & "]" into singleOrDoubleQuote
   replace "'" with singleOrDoubleQuote in someReg
   --
   if matchText (someHTML, someReg, metaContent) is true then
      return metaContent
   else
      return empty
   end if
end scrape_GetOgContent

function scrape_GetTwitterContent propName, someHTML
   /*
   <meta name="twitter:card" content="player">
   <meta name="twitter:card" content="summary_large_image" />
   <meta name="twitter:description" content="We don&#8217;t restrict you on any feature! Yes, everything is unlimited. No restriction on payments, submission, users and much more. Yearly (Get 4 Months Free) Monthly 1 Form Package $10 / mo Per Form Buy Now View All Features 3 Form Package $8 / mo Per Form Buy Now View All Features 10 Form Package &hellip; Continue reading &quot;Form Builder Pricing&quot;" />
   <meta name="twitter:title" content="Form Builder Pricing | Pabbly" />
   <meta name="twitter:image" content="https://www.pabbly.com/wp-content/uploads/2019/07/formbuilder.png" />
   */
   --
   put merge ("(?miU)<meta\s+name='twitter:[[propName]]'\s+content='(.*)'\s*/*>") into someReg
   put "['" & quote & "]" into singleOrDoubleQuote
   replace "'" with singleOrDoubleQuote in someReg
   --
   if matchText (someHTML, someReg, metaContent) is true then
      return metaContent
   else
      return empty
   end if
end scrape_GetTwitterContent


--> Working on
-
function scrape_FetchTwitterMeta contentKey, someURL
   put html_FetchTidy (someURL) into someHTML
   put revXMLCreateTree (someHTML, false, true, false) into treeID
   put scrape_TwitterContent (contentKey, treeID) into twitterNodeContent
   return twitterNodeContent
end scrape_FetchTwitterMeta

function scrape_TwitterContent contentKey, treeID
   put "twitter:" & contentKey into twitterKey
   put revXMLMatchingNode (treeID, "html/head", "meta", "name", twitterKey, -1, false) into twitterNode
   put word 1 to -1 of revXMLAttribute (treeID, twitterNode, "content") into twitterNodeContent
   return twitterNodeContent
end scrape_TwitterContent

function scrape_TwitterValue treeID
   put "twitter:player" into twitterKey
   put revXMLMatchingNode (treeID, "html/head", "meta", "name", twitterKey, -1, false) into twitterNode
   put word 1 to -1 of revXMLAttribute (treeID, twitterNode, "value") into twitterNodeContent
   return twitterNodeContent
end scrape_TwitterValue

function scrape_GetDiv someHtml, attributeName, attributeValue, pTidyIsTrue
   if pTidyIsTrue then html_Tidy someHtml
   put revXMLCreateTree (someHTML, false, true, false) into treeID
   if treeID is not a number then return treeID
   put scrapeID_GetDivText (treeID, attributeName, attributeValue) into someContent
   revXMLDeleteTree treeID
   return someContent
end scrape_GetDiv

function scrapeID_GetDivContents treeID, pAttributeName, pAttributeValue
   put scrapeID_GetDivNode (treeID, pAttributeName, pAttributeValue) into divNode
   if divNode is empty then return empty
   put revXMLNodeContents (treeID, divNode) into someContent
   return someContent
end scrapeID_GetDivContents

function scrapeID_GetDivText treeID, pAttributeName, pAttributeValue
   put scrapeID_GetDivNode (treeID, pAttributeName, pAttributeValue) into divNode
   if divNode is empty then return empty
   put revXMLText (treeID, divNode) into someContent
   return someContent
end scrapeID_GetDivText

function scrapeID_GetDivNode treeID, pAttributeName, pAttributeValue
   put revXMLRootNode (treeID) into rootNode
   put revXMLMatchingNode (treeID, rootNode, "div", pAttributeName, pAttributeValue, -1, false) into divNode
   return divNode
end scrapeID_GetDivNode


--> Scrape
-
function scrape_ContentSection someHtml
   -- this removes all html tags from the section
   put "//*[@id='content']" into someXPath
   put scrape_WithXPath (someHtml, someXPath, true) into contentSection
   return contentSection
end scrape_ContentSection

function scrape_ContentHeadings someHTML, pHeaderLevel
   put xpath_ConstructHeadingPath (pHeaderLevel, "content") into someXPath
   put scrape_WithXPath (someHtml, someXPath) into contentSection
   return contentSection
end scrape_ContentHeadings

function scrape_Headings someHTML, pHeaderLevel
   if pHeaderLevel is empty then put 3 into pHeaderLevel
   put xpath_ConstructHeadingPath (pHeaderLevel) into someXPath
   -- put format ("h%d", pHeaderLevel) into someXPath
   put scrape_WithXPath (someHtml, someXPath) into contentSection
   return contentSection
end scrape_Headings

function scrape_EvaluateContentHeadings someHTML, pHeaderLevel
   if pHeaderLevel is empty then put 3 into pHeaderLevel
   put format ("//*[@id='content']/h%d", pHeaderLevel) into someXPath
   
   put scrape_EvaluateXPath (someHtml, someXPath) into contentSection
   return contentSection
end scrape_EvaluateContentHeadings

function scrape_EvaluateXPath someHtml, someXPath, pTidyIsTrue
   if pTidyIsTrue then html_Tidy someHtml
   put revXMLCreateTree (someHTML, false, true, false) into treeID
   put revXMLEvaluateXPath (treeID, someXPath) into someContent
   revXMLDeleteTree treeID
   return someContent
end scrape_EvaluateXPath

function scrape_WithXPath someHtml, someXPath, pTidyIsTrue
   if pTidyIsTrue then html_Tidy someHtml
   put revXMLCreateTree (someHTML, false, true, false) into treeID
   put revXMLDataFromXPathQuery (treeID, someXPath) into someContent
   revXMLDeleteTree treeID
   return someContent
end scrape_WithXPath


--> Scrape | XML | OpenGraph
-
function scrapeID_GetContentSectionPath treeID
   put "//*[@id='content']" into someXPath
   put revXMLEvaluateXPath (treeID, someXPath) into contentSectionNode
   return line 1 of contentSectionNode
end scrapeID_GetContentSectionPath

command scrape_HtmlHeadings someHtml, @pageTitle, @handlerNames, @handerTitleNodes
   -- was "rig_ScrapeHeadings"
   
   if pTidyIsTrue then html_Tidy someHtml
   put revXMLCreateTree (someHTML, false, true, false) into treeID
   --
   put scrapeID_GetDivNode (treeID, "id", "content") into contentsDiv
   put xpath_ConstructHeadingPath (1, "content") into someXPath
   put word 1 to -1 of line 1 of revXMLDataFromXPathQuery (treeID, someXPath) into pageTitle
   -- 
   put xpath_ConstructHeadingPath (3, "content") into someXPath
   put word 1 to -1 of revXMLEvaluateXPath (treeID, someXPath) into handerTitleNodes
   put revXMLDataFromXPathQuery (treeID, someXPath) into handlerNames
   rig_CleanHandlerNames handlerNames
   
   put scrapeID_ConstructHeaderPageArray (treeID) into headerPageArray
   --
   revXMLDeleteTree treeID
   return headerPageArray
end scrape_HtmlHeadings

function scrapeID_ConstructHeaderPageArray treeID
   put scrapeID_GetContentSectionPath (treeID) into contentSectionPath
   put revXMLNumberOfChildren (treeID, contentSectionPath, "", 1) into numberOfChildren -- for safety
   put revXMLFirstChild (treeID, contentSectionPath, false) into nextNode
   
   put 1 into headerNum
   put 0 into numKeyList
   set the itemdelimiter to "/"
   repeat numberOfChildren -- for safety
      put item -1 of nextNode into someLeaf
      put token 1 of someLeaf into headerNum
      delete char 1 of headerNum
      switch  
         case (char 1 of someLeaf = "h" and headerNum is a number)  
            put revXMLText (treeID, nextNode, true) into titleBit
            put revXMLNodeContents (treeID, nextNode) into headerTitle
            
            put scrape_TidyTitle (titleBit, headerTitle) into headerTitle
            
            scrape_IncrementNumKeyList numKeyList, headerNum
            put scrape_ConstructNumKeyArray (numKeyList, headerNum) into numKeyArray
            
            put headerTitle into headerPageArray [numKeyArray]["@attributes"]["title"]
            put headerNum into headerPageArray [numKeyArray]["@attributes"]["level"]
            
            -- now put content into previous headers numKeyArray
            if htmlArray is an array then
               put htmlArray into headerPageArray [previousNumKeyArray]["@content"]
               delete variable htmlArray
            end if
            
            put numKeyArray into previousNumKeyArray
            break
         default
            put revXMLText (treeID, nextNode, true) into htmlBit
            array_AddToEndOfIndex htmlBit, htmlArray
      end switch
      
      put revXMLNextSibling (treeID, nextNode, false) into nextNode
      if nextNode is empty then
         exit repeat
      end if
   end repeat
   
   if htmlArray is an array then
      -- if theri is only one title - theri may be some left here to add to the top title
      put htmlArray into headerPageArray [previousNumKeyArray]["@content"]
      delete variable htmlArray
   end if
   return headerPageArray
end scrapeID_ConstructHeaderPageArray

command fedwiki_ParseMetaArray metaArray, @htmlTitle, @pageDescription, @imageURL, @articleTitle
   -- used with xml based 
   
   put metaArray ["title"] into htmlTitle
   if htmlTitle is empty or htmlTitle is an array then
      put metaArray ["title"]["content"] into htmlTitle
   end if
   --
   put metaArray ["property"]["og:title"]["content"] into articleTitle
   if articleTitle is empty then put htmlTitle into articleTitle
   --
   put metaArray ["description"]["content"] into pageDescription
   put metaArray ["property"]["og:description"]["content"]into ogDescription
   if pageDescription is empty then
      put ogDescription into pageDescription
   end if
   put metaArray ["property"]["og:image"]["content"] into imageURL
   if imageURL is empty then
      put metaArray ["twitter:image"]["content"] into imageURL
   end if
   
   if ogDescription is empty then
      return pageDescription
   else
      return ogDescription
   end if
   /*
   put metaArray ["author"]["content"] into contentAuthor
   put metaArray ["keywords"]["content"] into pageKeywords
   put metaArray ["property"]["article:modified_time"]["content"] into modifiedTime
   put metaArray ["property"]["article:author"]["content"] into articleAuthor
   
   put metaArray ["property"]["og:image:height"]["content"] into imageHeight
   put metaArray ["property"]["og:image:width"]["content"] into imageWidth
   put metaArray ["property"]["og:site_name"]["content"] into siteName
   put metaArray ["property"]["og:type"]["content"] into pageType
   put metaArray ["property"]["og:url"]["content"] into pageURL
   */
end fedwiki_ParseMetaArray

private function scrape_TidyTitle titleBit, headerTitle
   replace CR with empty in titleBit
   put replacetext (titleBit, "<[^><]*>","") into headerTitle
   return headerTitle
end scrape_TidyTitle

private command scrape_IncrementNumKeyList @numKeyList, headerNum
   put item headerNum of numKeyList into lastCount
   add 1 to lastCount
   put lastCount into item headerNum of numKeyList
   return lastCount
end scrape_IncrementNumKeyList

private function scrape_ConstructNumKeyArray numKeyList, headerNum
   repeat with itemNum =1 to headerNum
      if item itemNum of numKeyList is empty then
         put 1 into item itemNum of numKeyList
      end if
   end repeat
   
   put item 1 to headerNum of numKeyList into numKeyList
   split numKeyList by comma
   return numKeyList
end scrape_ConstructNumKeyArray


--> XPath
-
/*
Handlers that construct XPath queries
*/

function xpath_ConstructHeadingPath pHeaderLevel, pBelowAttributeID
   if pHeaderLevel is empty then put 3 into pHeaderLevel
   if pBelowAttributeID is empty then
      put format ("h%d", pHeaderLevel) into someXPath
   else
      put format ("//*[@id='%s']/h%d", pBelowAttributeID, pHeaderLevel) into someXPath
   end if
   return someXPath
end xpath_ConstructHeadingPath


--> Private
-
private command _Utf8Decode utf8Text
   set the htmltext of the templatefield to utf8Text
   put the unicodetext of the templatefield into someUTF16
   put unidecode (someUTF16, "ANSI") into utf8Text
end _Utf8Decode

private command _ScrapeSetPageInfo headerArray, @pageTitle, @pageDescription, @imageURL
   /*
   put metaArray ["author"]["content"] into contentAuthor
   put metaArray ["keywords"]["content"] into pageKeywords
   put metaArray ["property"]["article:modified_time"]["content"] into modifiedTime
   put metaArray ["property"]["article:author"]["content"] into articleAuthor
   
   put metaArray ["property"]["og:image:height"]["content"] into imageHeight
   put metaArray ["property"]["og:image:width"]["content"] into imageWidth
   put metaArray ["property"]["og:site_name"]["content"] into siteName
   put metaArray ["property"]["og:type"]["content"] into pageType
   put metaArray ["property"]["og:url"]["content"] into pageURL
   
   -- fedwiki_ParseMetaArray metaArray, htmlTitle, pageDescription, imageURL, articleTitle
   */
   
   put headerArray ["og"]["title"] into pageTitle
   if pageTitle is empty then put headerArray ["twitter"]["title"] into pageTitle
   if pageTitle is empty then put headerArray ["title"] into pageTitle
   _Utf8Decode pageTitle
   
   put headerArray ["og"]["description"] into pageDescription
   if pageDescription is empty then put headerArray ["twitter"]["description"] into pageDescription
   if pageDescription is empty then put headerArray ["description"] into pageDescription
   if pageDescription is empty then put pageTitle into pageDescription   
   
   put headerArray ["og"]["image"] into imageURL
   if imageURL is empty then put headerArray ["twitter"]["image"] into imageURL
   --
   return headerArray
end _ScrapeSetPageInfo
